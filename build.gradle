buildscript {
    ext {
        springBootVersion = '2.2.0.RELEASE'
        querydslPluginVersion = '1.0.10'
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // plugin 저장소
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}")
    }
}

//전체 프로젝트에 적용될 설정
allprojects {

}

//모든 하위 프로젝트에 공통으로 적용될 설정
subprojects {
    group = 'com.ccs'
    version = '1.0.0'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'
    apply plugin: "com.ewerk.gradle.plugins.querydsl"

    sourceCompatibility = '1.8'

    repositories {
        mavenCentral()
    }
    dependencies {
        compile("com.querydsl:querydsl-jpa") // querydsl
        compile("com.querydsl:querydsl-apt") // querydsl
        testCompile group: 'junit', name: 'junit', version: '4.12'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    def querydslSrcDir = 'src/main/generated'

    querydsl {
        library = "com.querydsl:querydsl-apt"
        jpa = true
        querydslSourcesDir = querydslSrcDir
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', querydslSrcDir]
            }
        }
    }

    compileQuerydsl{
        options.annotationProcessorPath = configurations.querydsl
    }


    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        querydsl.extendsFrom compileClasspath
    }
}

project(':professorlol-web') {   //application 모듈
    dependencies {
        compile project(':professorlol-riot-api')   // 독립 모듈
        compile project(':professorlol-core')       // 공통 모듈
        compile project(':professorlol-crawling')
        compile project(':professorlol-common')
    }
}

/**
 * main메소드가 필요없는 모듈의 경우 build.gradle에
 * bootJar { enabled = false}* jar {enabled = true}*/

